#!/bin/zsh

set -e

db=/var/log/httpd/dflog.db
opts=()
[[ -w $db ]] || opts+=(--ro)
while ((ARGC)) && [[ $1 == --* ]]; do
    opts+=($1); shift
done

realscript=$(readlink -f $0)
driver=$realscript:h/DiskAlert.pm
function run_driver {
    $driver --db=$db $opts $argv
}

#----------------------------------------
# fs 配列には
# mountpoint opt=value ...
# を詰める

fs=(
    /var/www
      min=1G decr1=100M

    /home
      min=1G decr1=100M

    /var
      min=1G decr1=10M

    /var/log/httpd
      min=1G decr1=10M

    /
      min=1G
)

# min は, 残り容量の最小値.
# decr1 は, 前回(=前日)からの減少量のしきい値

#----------------------------------------
# XXX: $argv[1] =~ /^\w/ なら別コマンド.

function cmd_ls {
    if ((ARGC)); then
	run_driver list_growth $argv
    else
	run_driver list_disks
    fi
}

function cmd_plot {
    local plot="plot \"<  $realscript ls $*\" using 2:4"
    gnuplot $realscript:r.gnuplot =(print -r $plot) -
}

if ((ARGC)); then
    cmd=$1; shift
    if (($+functions[cmd_$cmd])); then
	cmd_$cmd $argv
    else
	run_driver $cmd $argv
    fi
else
    run_driver watch $fs
fi
